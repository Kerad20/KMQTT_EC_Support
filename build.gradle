plugins {
    id 'org.jetbrains.kotlin.multiplatform' version '1.3.61'
    id 'maven-publish'
}

group 'io.kmqtt'
version '0.0.1'

repositories {
    mavenCentral()
    jcenter()
}

kotlin {
    jvm {
        compilations.main.kotlinOptions {
            // Setup the Kotlin compiler options for the 'main' compilation:
            jvmTarget = "1.8"
        }
        compilations.test.kotlinOptions {
            // Setup the Kotlin compiler options for the 'test' compilation:
            jvmTarget = "1.8"
        }
    }
    mingwX64("mingw") {
        compilations.main {
            cinterops {
                openssl {
                    packageName 'openssl'
                }
            }
        }
        binaries {
            executable() {
                linkerOpts = ['-L/msys64/home/39340/openssl-1.1.1d/dist/lib', '-lssl', '-lcrypto']
            }
        }
    }
    linuxX64() {
        binaries {
            executable()
        }
    }
    linuxArm32Hfp() {
        binaries {
            executable()
        }
    }

    sourceSets {
        all {
            languageSettings {
                useExperimentalAnnotation('kotlin.ExperimentalStdlibApi')
                useExperimentalAnnotation('kotlin.ExperimentalUnsignedTypes')
            }
        }
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }
        jvmMain {
            dependencies {
                implementation kotlin('stdlib-jdk8')
            }
        }
        jvmTest {
            dependencies {
                implementation kotlin('test')
                implementation kotlin('test-junit')
                implementation 'com.hivemq:hivemq-mqtt-client:1.1.3'
            }
        }
        mingwMain {

        }
        mingwTest {

        }
        linuxX64Main {

        }
        linuxX64Test {

        }
        linuxArm32HfpMain {

        }
        linuxArm32HfpTest {

        }
    }
}

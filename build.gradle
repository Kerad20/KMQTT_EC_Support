import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'org.jetbrains.kotlin.multiplatform' version '1.3.61'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

group 'io.kmqtt'
version '0.0.2'

repositories {
    mavenCentral()
    jcenter()
}

kotlin {
    jvm {
        compilations.main.kotlinOptions {
            // Setup the Kotlin compiler options for the 'main' compilation:
            jvmTarget = "1.8"
        }
        compilations.test.kotlinOptions {
            // Setup the Kotlin compiler options for the 'test' compilation:
            jvmTarget = "1.8"
        }
    }
    mingwX64("mingw") {
        compilations.main {
            cinterops {
                openssl {
                    packageName 'openssl'
                }
            }
        }
        binaries {
            executable() {
                linkerOpts = ['-Lsrc/nativeInterop/cinterop/opensslLibs/mingwX64', '-lssl', '-lcrypto']
            }
        }
    }
    linuxX64() {
        binaries {
            executable()
        }
    }
    linuxArm32Hfp() {
        binaries {
            executable()
        }
    }

    sourceSets {
        all {
            languageSettings {
                useExperimentalAnnotation('kotlin.ExperimentalStdlibApi')
                useExperimentalAnnotation('kotlin.ExperimentalUnsignedTypes')
            }
        }
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }
        jvmMain {
            dependencies {
                implementation kotlin('stdlib-jdk8')
            }
        }
        jvmTest {
            dependencies {
                implementation kotlin('test')
                implementation kotlin('test-junit')
                implementation 'com.hivemq:hivemq-mqtt-client:1.1.3'
            }
        }
        mingwMain {

        }
        mingwTest {

        }
        linuxX64Main {
            dependencies {
                implementation files('src/nativeInterop/openssl-linux-x64.klib')
            }
        }
        linuxX64Test {

        }
        linuxArm32HfpMain {
            dependencies {
                implementation files('src/nativeInterop/openssl-linux-arm32-hfp.klib')
            }
        }
        linuxArm32HfpTest {

        }
    }
}

task shadowJar(type: ShadowJar) {
    manifest.attributes['Main-Class'] = 'MainKt'
    from kotlin.targets.jvm.compilations.main.output
    def runtimeClasspath = kotlin.targets.jvm.compilations.main.runtimeDependencyFiles
    configurations = [runtimeClasspath]
}
